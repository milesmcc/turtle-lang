WHITESPACE = _{ " "+ | "\t"+ | NEWLINE+ }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | ("//" | ";;" ) ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

// Primitives
list = { "(" ~ expression* ~ ")" }
symbol = @{ (LETTER | NUMBER | SYMBOL | DASH_PUNCTUATION)+ }
keyword = { ":" ~ symbol }
number = @{ "-"? ~ (ASCII_DIGIT)+ ~ ("." ~ ASCII_DIGIT+)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
text = { "\"" ~ char* ~ "\"" }

primitive = _{ list | keyword | text | number | symbol }

// Functions & macros
arg_symbols = { "(" ~ symbol* ~ ")" }
lambda = {"(" ~ "lambda" ~ arg_symbols ~ expression+ ~ ")"}
macro_ = {"(" ~ "macro" ~ arg_symbols ~ expression+ ~ ")"}

// Sugar
quote = { "'" ~ expression }
eval = { "," ~ expression }
sugar = _{ quote | eval }

expression = _{ sugar | lambda | macro_ | primitive }

expressions = _{ (WHITESPACE | COMMENT)* ~ expression+ }